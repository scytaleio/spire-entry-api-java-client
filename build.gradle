buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'com.google.protobuf', name: 'protobuf-gradle-plugin', version: '0.8.13'
    }
}

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "com.google.osdetector" version "1.6.2"
}

group 'org.example'
version '0.1.0'

apply plugin: 'com.google.protobuf'
apply plugin: 'com.github.johnrengelman.shadow'

description = "Functionality to interact with the admin Entry API"

ext {
    grpcVersion = '1.33.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.13.0'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

assemble.dependsOn shadowJar

shadowJar {
    archiveClassifier = osdetector.classifier
    manifest {
        attributes 'Main-Class': 'io.spiffe.entryapi.EntryAdmin'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'io.grpc', name: 'grpc-netty', version: "${grpcVersion}"
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: "${grpcVersion}"
    implementation group: 'io.grpc', name: 'grpc-stub', version: "${grpcVersion}"
    compileOnly group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    implementation group: 'io.spiffe', name: 'java-spiffe-provider', version: '0.6.3'

    if (osdetector.os.is('osx') ) {
        runtimeOnly group: 'io.spiffe', name: 'grpc-netty-macos', version: '0.6.3'
    }

    implementation group: 'info.picocli', name: 'picocli', version: '4.5.2'
    annotationProcessor group: 'info.picocli', name: 'picocli-codegen', version: '4.5.2'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: "1.18.16"
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version:"1.18.16"

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}
